init module {
	knowledge {
		% A room is a place with exactly one neighbour, i.e., there is only one way to get to and from that place.
		room(PlaceID) :- zone(_,PlaceID,_,_,Neighbours), length(Neighbours,1).
		
		% Exercise 2.2: insert a definition of the predicate "nextColorInSeq(Color)".
		nextColorInSeq(Color) :- sequenceIndex(Index), sequence(Seq), nth0(Index, Seq, Color).

		% Exercise 2.5 + 2.6: (optional) if needed, insert additional knowledge here.
		
		% Gives the next block needed in the sequence when in a room, if any
		nextBlockInSeq(BlockID) :- nextColorInSeq(Color), color(BlockID, Color).
		
	}
	program {
		% Store map information, i.e., navigation points in the agent's belief base.
		forall bel( percept(zone(ID, Name, X, Y, Neighbours)) ) do insert( zone(ID, Name, X, Y, Neighbours) ).

		% Record the initial state of movement in belief base.
		if bel( percept(state(State)) ) then insert( state(State) ).
		
		% Adopt initial goals.
		forall bel( room(Place) ) do adopt( in(Place) ).
		
		% TODO % if bel( percept(sequenceIndex(Index))) then adopt(sequenceIndex(Index)). % Needs to be initialized

		% Exercise 2.1a: insert rules for processing percepts that are sent only at the beginning of a session, 
		%  and/or need to be initialized.
		if bel( percept(at(Place))) then insert(at(Place)). % Needs to be initialized
		forall bel( percept(place(Place))) do insert(place(Place)). % Send once
		if bel( percept(sequence(List))) then insert(sequence(List)). % Send once
		if bel( percept(sequenceIndex(Index))) then insert(sequenceIndex(Index)). % Needs to be initialized
		
	}	
	actionspec {
		% The goTo action makes the agent move to a place (location) in the BW4T environment.
		% As long as the agent has not arrived at the place it is going to, it will be in "traveling" mode.
		goTo(Location) {
            pre { place(Location), not(state(traveling)) }
			post { true }
		}

		% Exercise 2.3a: insert a specification for the goToBlock(Block) action.
		goToBlock(Block) {
			pre { not(state(traveling)), inRoomBlock(Block) }
			post { true }
		}

		% Exercise 2.3b: insert a specification for the pickUp action.
		pickUp {
			pre { not(state(traveling)), not(holding(_)), atBlock(_) }
			post { true }
		}

		% Exercise 2.3c: insert a specification for the putDown action.
		putDown {
			pre { not(state(traveling)), holding(_) }
			post { true }
		}
	}
}

main module [exit=nogoals] {
	program {
		% Exercise 2.5a: insert code for action rules that allows an agent to systematically search for blocks.
		% If we believe that we currently see a color with the needed color, go to the block, if we are not already there
		if bel( nextBlockInSeq(BlockID)), bel( not(holding(_))) then goToBlock(BlockID).

		% Try to pickup a block when we are at the block with the needed color
		if bel( nextBlockInSeq(BlockID) ), bel( atBlock(BlockID)) then pickUp.
		
		% If we are holding anything, return to the DropZone
		if bel( not(in('DropZone'))), bel( holding(_) ) then goTo('DropZone'). % Hardcoded dropzone
		if bel( in('DropZone')) then putDown. % If we are in dropzone, always putDown block

		% Added move items below the rest
		% If we have a right color in the room, don't move away
		if bel( not(nextBlockInSeq(_)) ), a-goal( in(Place) ) then goTo(Place).

		% Exercise 2.6a: insert code for action rules that makes the agent deliver a block when it 
		%   knows about a block that can be delivered.
		
	}
}

event module {
	program {
	    % Update the agent's state of movement.
		forall bel( state(State), percept(state(NewState)) ) do delete( state(State) ) + insert( state(NewState) ).
		
		% Record when we are entering or leaving a room.
		forall bel( percept(in(Place)) ) do insert( in(Place) ).
		forall bel( percept(not(in(Place))) ) do delete( in(Place) ) + insert( visited(Place) ). % Updated with visited predicate into belief base

		% Exercise 2.1b: insert rules for handling percepts other than "sendonce" percepts.
		forall bel( at(Place), percept(at(NewPlace)) ) do delete( at(Place) ) + insert( at(NewPlace) ). % Send on change
		
		forall bel( percept(atBlock(BlockID)) ) do insert( atBlock(BlockID) ). % Send on change with negation
		forall bel( percept(not(atBlock(BlockID))) ) do delete( atBlock(BlockID) ).
		
		forall bel( inRoomBlock(BlockID), percept(color(NewBlockID, _)) ) do delete( inRoomBlock(BlockID))+ insert(inRoomBlock(NewBlockID)). % Send on change local
		
		forall bel( color(BlockID, Color) ) do delete(color(BlockID, Color)). % Remove all colors, because the ones we can currently see are sent
		forall bel( percept(color(BlockID, Color)) ) do insert(color(BlockID, Color)). % Send always, local
		
		forall bel( percept(holding(BlockID)) ) do insert( holding(BlockID) ). % Send on change with negation
		forall bel( percept(not(holding(BlockID))) ) do delete( holding(BlockID) ).
		
		forall bel( sequenceIndex(Index), percept(sequenceIndex(NewIndex)) ) do delete( sequenceIndex(Index) ) + insert( sequenceIndex(NewIndex) ).
		
		% Exercise 2.5b: insert code for goal management that allows an agent to systematically search for blocks. (if needed)
		
		% Exercise 2.6b: insert code for goal management that makes the agent deliver a block when it 
		%   knows about a block that can be delivered. (if needed)
		
	}
}
